buildscript {
    ext.kotlin_version = '1.2.21'

    repositories {
        mavenCentral()
        mavenLocal()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'net.researchgate.release' version '2.6.0'
    id "org.jetbrains.kotlin.jvm" version '1.2.21'
}

release {
    failOnCommitNeeded = false
    failOnPublishNeeded = true
    failOnSnapshotDependencies = true
    failOnUnversionedFiles = true
    failOnUpdateNeeded = true
    revertOnFail = true

    git {
        requireBranch = 'master'
        pushToRemote = 'origin'
    }
}

ext {
    TORNADO_FX_VERSION = "1.7.15"
    TORNADO_FX_CONTROLS_FXVERSION = "0.1"
    JFOENIX_VERSION = "9.0.2"
    SLF4J_VERSION = "1.8.0-alpha2"
}

allprojects {
    group 'ez.snipes'

    apply plugin: 'java'
    apply plugin: 'kotlin'
    apply plugin: 'maven'
    apply plugin: 'signing'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenCentral()
        mavenLocal()
        jcenter()
    }

    if(JavaVersion.current().isJava8Compatible()) {
        tasks.withType(Javadoc) {
            options
        }
    }

    sourceSets {
        main.kotlin.srcDirs =  [ 'src/main/kotlin' ]
        main.java.srcDirs = [ 'src/main/java' ]
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
        compile "org.slf4j:slf4j-simple:$SLF4J_VERSION"
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives jar
        archives javadocJar
        archives sourcesJar
    }

    signing {
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: NEXUS_USERNAME, password: NEXUS_PASSWORD)
                }
                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: NEXUS_USERNAME, password: NEXUS_PASSWORD)
                }
                pom.project {
                    name project.name
                    packaging 'jar'
                    description 'A playground Wallpaper Application.'
                    url 'https://github.com/ezsnipes/ez.wollz'

                    scm {
                        connection 'scm:git:git://github.com/ezsnipes/ez.wollz.git'
                        developerConnection 'scm:git:git://github.com/ezsnipes/ez.wollz'
                        url 'http://github.com/ezsnipes/ez.wollz'
                    }

                    licenses {
                        license {
                            name 'Apache-2.0'
                            url 'https://raw.githubusercontent.com/ezsnipes/ez.wollz/master/LICENSE'
                        }
                    }

                    developers {
                        developer {
                            id 'ezsnipes'
                            name 'ez.snipes-'
                            email '34411645+ezsnipes@users.noreply.github.com'
                        }
                        developer {
                            id 'pddstudio'
                            name 'Patrick Jung'
                            email 'patrick.pddstudio@gmail.com'
                        }
                    }
                }
            }
        }
    }
}
